#
# Timer
#

every 8 seconds in world "ul_prox":
  set {_t::*} to {spawners::*} where [spawnMockReturn(input) = ""]

#
# Spawning
#

command /spawnmob <text>:
  permission: op
  trigger:
    spawn skeleton at location of player
    set last spawned skeleton's display name to arg-1

function spawnReturn(loc: location):
  if size of all players in radius {range::%{_loc}%} around {_loc} >= 1:
    if size of all monsters in radius 30 around {_loc} < {max::%{_loc}%}:
      wait ("%random integer between 1 and 8% seconds" parsed as timespan)

      if {type::%{_loc}%} = "undeadtwig":
        spawnEntity(adult zombie, {_loc}, "&fUndead Twig", 8)
        equipEntity(last spawned zombie, air, button, air, air, leather boots dyed green)

      if {type::%{_loc}%} = "undeadbranch":
        spawnEntity(adult zombie, {_loc}, "&fUndead Branch", 15)
        equipEntity(last spawned zombie, stick, button, air, leather leggings dyed green, leather boots dyed green)

      if {type::%{_loc}%} = "rattler":
        spawnEntity(skeleton, {_loc}, "&fRattler", 25)
        equipEntity(last spawned skeleton, bone)
      
      if {type::%{_loc}%} = "acolyte":
        spawnEntity(skeleton, {_loc}, "&fAcolyte", 20)
        equipEntity(last spawned skeleton, bone of efficiency 1)

#
# Abilities
#

on spawn:
  wait 1 ticks
  set {_n} to uncolored name of event-entity
  set {_u} to uuid of event-entity
  if event-entity is a skeleton:
    if {_n} contains "Acolyte":
      while event-entity is alive:
        if event-entity's target is set:
          play sound "entity.blaze.shoot" at volume 1 and pitch 2 at location of event-entity
          drawLine particle redstone, RGB 255, 0, 255, center location of block 1 above event-entity, target location 1.5 above event-entity's target, id "%{_u}%.ability", solid true, density 10, visibleRange 32
          set {_r} to random number between 1.0 and 3.5
          damage event-entity's target by {_r}
        wait 2 ticks
        stopEffect id "%{_u}%.ability"
        wait 5 seconds


#
# Functions
#

function spawnMockReturn(loc: location) :: text:
  spawnReturn({_loc})
  set {_r} to ""
  return {_r}

function spawnEntity(x: entitytype, loc: location, n: text, h: number):
  spawn {_x} at {_loc}
  set {_e} to last spawned entity
  set displayname of last spawned entity to "%{_n}% &6%{_h}%"
  set {_e}'s max health to {_h}
  set {_e}'s health to {_h}

function equipEntity(e: entity, t: item=air, h: item=air, c: item=air, l: item=air, b: item=air):
  set {_e}'s helmet to {_h}
  set {_e}'s chestplate to {_c}
  set {_e}'s leggings to {_l}
  set {_e}'s boots to {_b}
  set {_e}'s tool to {_t}