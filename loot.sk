#
# Chests
#

on click on chest:
  if player's gamemode is not creative:
    cancel event
    if {looted::%player's uuid%::%location of event-block%} is not set:
      loop all items in inventory of event-block:
        if name of loop-item contains "coins":
          set {_split::*} to name of loop-item split at "coins"
          set {_c} to {_split::1} parsed as integer
          add {_c} to {coins::%player's uuid%}
          send "&7You found &6%{_c}% &6coins&7!"
        else:
          add name of loop-item to {_list::*}
          give player loop-item
        add 1 to {_total}
      set {_s} to size of {_list::*}
      if {_s} > 0:
        send "&7You found &f%{_list::*}%&7!"
      if {_total} > 0:
        set {looted::%player's uuid%::%location of event-block%} to 1
        play sound "block.chest.open" at volume 1 and pitch 1 at location of player
      else:
        send "&7This chest is empty, or maybe has negative items, cool!"
    else:
      send "&7You already looted this chest!"
      play sound "entity.villager.no" at volume 1 and pitch 1 at location of player

command /lootreset <offlineplayer>:
  permission: op
  trigger:
    delete {looted::%arg-1's uuid%::*}
    send "&7Loot is now reset for &f%arg-1%&7!"

#
# Mobs
#

on pickup:
  if event-item is gold nugget:
    cancel event
    set {_loc} to location of event-entity
    set {_a} to item amount of event-item
    add {_a} to {coins::%player's uuid%}
    play sound "entity.item.pickup" at volume 1 and pitch 2 at location of player
    drawDot count 5, particle "redstone", speed 1, RGB 255, 255, 0, center {_loc}, rainbowMode false, visibleRange 32, pulseDelay 0, keepFor 2 ticks
    delete event-entity

on death:
  if attacker is not a villager:
    clear drops
    set {_r1} to random integer between 0 and 2
    set {_r2} to random integer between 0 and 5
    set {_r3} to random integer between 0 and 10
    set {_r4} to random integer between 0 and 20
    set {_n} to uncolored display name of victim
    if {_n} contains "Undead Twig":
      drop {_r1} of gold nuggets at victim
      chance of 30%:
        drop 1 stick named "&fTwig" lored "&7Material" at victim
    if {_n} contains "Undead Branch":
      drop {_r1} of gold nuggets at victim
      chance of 60%:
        drop 1 stick named "&fTwig" lored "&7Material" at victim
      chance of 20%:
        drop 1 stick named "&fTwig" lored "&7Material" at victim
    if {_n} contains "Rattler":
      drop {_r1} of gold nuggets at victim
      chance of 40%:
        drop 1 bone named "&fBone" lored "&7Material" at victim
    if {_n} contains "Acolyte":
      drop {_r2} of gold nuggets at victim
      chance of 10%:
        drop {_r3} of gold nuggets at victim



